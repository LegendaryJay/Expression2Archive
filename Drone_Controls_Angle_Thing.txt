@name Drone Controls Demo
@inputs Seat:entity
@outputs EyeEnable ForceAng EyePos:vector EyeDir:vector E
@persist E TarPitch TarRoll AngAmp AccAmp RollAcc PitchAcc RollAdd PitchAdd PitchAmp ForceAngAdd SpawnPlace:vector
@trigger 
@model models/props_junk/sawblade001a.mdl
if (first()){
    runOnTick(1)    
    entity():setMass(400)
    AngAmp=1
    AccAmp=0.0001
    E=entity()
    
    SpawnPlace=entity():pos()
    entity():propFreeze(0)
}
Driver=Seat:driver()
if (Driver){EyeEnable=1}
else {EyeEnable=0}
LClick=Driver:keyAttack1()
RClick=Driver:keyAttack2()
R=Driver:keyReload()
W=Driver:keyPressed("w")
A=Driver:keyPressed("a")
D=Driver:keyPressed("d")
S=Driver:keyPressed("s")
Shift=Driver:keyPressed("LSHIFT")
Ctrl=Driver:keyPressed("LCONTROL")
Space=Driver:keyPressed("Space")

if (W|S){
    if (W){PitchAdd+=AngAmp}
    else {PitchAdd-=AngAmp}
    PitchAcc+=AccAmp
    TarPitch= PitchAdd*PitchAcc
}
else{
    PitchAdd=0
    PitchAcc=1
    TarPitch=PitchAdd*PitchAcc - entity():angVel():pitch()*0.7
}
if (A|D){
    if (D){RollAdd+=AngAmp}
    else {RollAdd-=AngAmp}
    RollAcc+=AccAmp
    TarRoll= RollAdd*RollAcc
}
else{
    RollAdd=0
    RollAcc=1
    TarRoll=RollAdd*RollAcc - entity():angVel():roll()*0.7
}
ForceAngAdd=0.05
if (Space|Shift){
    ForceAngAdd = (Space ? ForceAngAdd : -ForceAngAdd)
    ForceAng+=ForceAngAdd
}


if (ForceAng > 15){
    ForceAng=15
}
if (ForceAng < 0){
    ForceAng = 0
}

if (R){
    ForceAng=0
    RollAdd=0
    RollAcc=1
    PitchAdd=0
    PitchAcc=1
    entity():setAng(ang())
    entity():setPos(SpawnPlace)
}
Force = (ForceAng/15)*10500
TargetAng=(ang(TarPitch,0,TarRoll))
entity():applyTorque(vec(TarRoll,TarPitch,-entity():angVel():yaw())*1)
entity():applyForce(entity():up()*Force)
EyePos= entity():toWorld(vec(10,0,20))
EyeDir= entity():forward()
